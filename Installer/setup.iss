; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define ReleaseDir "D:\Projects\BTAF\BTAF.Service\bin\Release"
#define MyAppName "BTAF"
#define MyAppVersion "1.0"
#define MyAppPublisher "Kevin Gut"
#define MyAppURL "https://github.com/AyrA/BTAF"
#define MyAppExeName "BTAF.Service.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{10F62951-C246-48C7-A4A3-85B370829495}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={autopf}\{#MyAppName}
UninstallDisplayIcon={app}\{#MyAppExeName}
DefaultGroupName={#MyAppName}
LicenseFile=D:\Projects\BTAF\LICENSE.txt
PrivilegesRequired=admin
OutputBaseFilename=BTAF Setup
SetupIconFile=D:\Projects\BTAF\BTAF.Service\app.ico
SolidCompression=yes
WizardStyle=modern

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "{#ReleaseDir}\{#MyAppExeName}"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#ReleaseDir}\BTAF.Lib.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#ReleaseDir}\BTAF.Lib.pdb"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#ReleaseDir}\BTAF.Service.exe.config"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#ReleaseDir}\BTAF.Service.pdb"; DestDir: "{app}"; Flags: ignoreversion

[Icons]
Name: "{group}\BTAF"; Filename: "{app}\{#MyAppExeName}"
Name: "{group}\Uninstall"; Filename: "{uninstallexe}"
Name: "{autodesktop}\BTAF"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Run]
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent runascurrentuser

[UninstallRun]
Filename: "{app}\{#MyAppExeName}"; Parameters: "/UNINSTALL"; RunOnceId: "DelService"; Flags: runascurrentuser

; 1. Delete config.bin from the application directory if the user wants to
; 2. Force removal of the application directory if it's empty after the uninstall process completes (inno setup is too stupid to do it by itself)
[Code]
procedure CurUninstallStepChanged(CurUninstallStep: TUninstallStep);
begin
  if CurUninstallStep = usUninstall then
  begin
    if FileExists(ExpandConstant('{app}\config.bin')) then
      if TaskDialogMsgBox('Delete BTAF configuration file?', 'If you are permanently removing this application, delete the configuration. Otherwise it should be kept', mbInformation, MB_YESNO, ['Delete configuration', 'Keep configuration'], 0) = IDYES
      then
        DeleteFile(ExpandConstant('{app}\config.bin'));
  end;
  if CurUninstallStep = usPostUninstall then
  begin
    if DirExists(ExpandConstant('{app}')) then
      DelTree(ExpandConstant('{app}'), True, False, False);
  end;
end;
